% --- 36 msbr channels --------------------------------------

set title "MSBR channels"

% --- Fuel pin:

surf 1 cyl 0 0 0.762 % hole in moderator (fuel+salt) in zone I-A (Robertson)
surf 2 pz  -198.12
surf 3 pz  198.12

cell  1  1  fuel      -1 2 -3
cell  2  1  moder     1  2 -3
cell  3  1  outside   -2
cell  4  1  outside   3

% --- Lattice (6x6, round molten salt+fuel channel and graphite around)
% square lattice=1, x0=0, y0=0, 6 el in x-dir&y-dir, grid step 10.16 cm
lat 300 1 0 0 6 6 10.16
1 1 1 1 1 1
1 1 1 1 1 1
1 1 1 1 1 1
1 1 1 1 1 1
1 1 1 1 1 1
1 1 1 1 1 1

% --- Surfaces:

surf 3000  sqc 0.0 0.0 30.48 % r=(grid step/2) * number of elements
surf 3001  pz  -198.12
surf 3002  pz  198.12

% --- Cells:

cell 300  0    fill 300  -3000 3001 -3002
cell 301  0    outside   3000  3001 -3002
cell 302  0    outside   -3001
cell 303  0    outside   3002

% --- Fuel (7LiF-BeF2-ThF4-233UF4):

mat fuel    -3.3304 tmp 908 % g/cm3 for mixture
 3007.09c   -19.2282   % Mass fraction of Li-7
 9019.09c   -68.13734    % Mass fraction of F-19
 4009.09c   -3.06718    % Mass fraction of Be-9
% 40000.09c   -10.92 % Zirconium
%92233.09c   -0.00226223   % Mass fraction of U-233 (0.3% mole)
92233.09c   -0.527854   % Mass fraction of U-233 (0.7% mole)
90232.09c   -9.0393263    % Mass fraction of Th-232

% --- Moderator graphite:

mat moder    -1.843 moder gr 6012 %from Robertson
 6012.09c    -1.0

% --- Thermal scattering data for graphite:

therm gr grj3.12t

% --- Cross section data library file path:

set acelib "/home/andrei2/serpent/xsdata/endfb7/sss_endfb7.xsdata"
%set acelib "/home/andrei2/serpent/xsdata/jeff311/sss_jeff311.xsdata"
% --- Periodic boundary condition:

set bc 3

% --- group constant generation:

% universe = 0 (homogenization over all space)
% symmetry = 0
% 4-group structure (3 group boundaries)

set gcu  0
set sym  0
%set nfg  4  0.625E-6  5.5E-3  0.821
set nfg  2 0.625E-6
% --- Neutron population and criticality cycles:

set pop 2000 500 20

% --- Geometry and mesh plots:

plot 3 500 500
plot 1 500 500 4.695
mesh 3 500 500

% ------------------------------------------------------------

% --- Detector energy grid (uniform lethargy):

ene 1 3 1000 1E-10 12.0
ene 2 2 1000 1E-10 12.0
ene 3 4 nj20 % ecco 1968-group structure
% --- Flux per lethargy:

det 1 de 1 dt -3

% --- Differential capture, fission and production spectra:

det 2 de 1 dt -2 dr -2 void
det 3 de 1 dt -2 dr -6 void
det 4 de 1 dt -2 dr -7 void

% --- Integral capture, fission and production spectra:

det 5 de 1 dt -1 dr -2 void
det 6 de 1 dt -1 dr -6 void
det 7 de 1 dt -1 dr -7 void
% Total flux in material 
det 8 de 1 dt -2 % by energy width
det 9 de 2 dt -2 % no, strange
det 10 de 3 dt -2 % by energy width

det 11 de 1 dt -1 % cumulative flux
det 12 de 2 dt -1 % same as 11
det 13 de 3 dt -1 % same as 11 (maybe more accurate, because almost 2000 groups)
% --- Average flux in moderator:
det 14 de 3 dm moder
det 15 de 2 dm moder %no, stange
det 16 de 1 dm moder
% --- Average flux in fuel:
det 17 de 3 dm fuel
det 18 de 2 dm fuel %no
det 19 de 1 dm fuel
%--------------------------------------------------------
